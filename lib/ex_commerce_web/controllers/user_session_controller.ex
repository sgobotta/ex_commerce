defmodule ExCommerceWeb.UserSessionController do
  use ExCommerceWeb, :controller

  alias ExCommerce.Accounts
  alias ExCommerceWeb.UserAuth

  action_fallback ExCommerceWeb.UserFallbackController

  def new(conn, _params) do
    render(conn, "new.html", error_message: nil)
  end

  def create(conn, %{"user" => user_params} = params) do
    with :ok <- UserAuth.validate_recaptcha(params) do
      %{"email" => email, "password" => password} = user_params

      if user = Accounts.get_user_by_email_and_password(email, password) do
        UserAuth.log_in_user(conn, user, user_params)
      else
        render(conn, "new.html",
          error_message: dgettext("errors", "Invalid email or password")
        )
      end
    end
  end

  def delete(conn, _params) do
    conn
    |> put_flash(:info, gettext("Logged out successfully."))
    |> UserAuth.log_out_user()
  end
end
